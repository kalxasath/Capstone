apply plugin: 'com.android.application'

// Begin of reading the sensitive data
// GV 21/02/2018
def noKeyFound = '"NO_KEY_FOUND"'
def youtubeApiKey = noKeyFound

def sensitiveDataFile = file('../sensitive_data.txt')
def Properties sensitiveData = new Properties()

if (sensitiveDataFile.canRead()){
    // Read the sensitive data from file sensitive_data.txt
    sensitiveData.load(new FileInputStream(sensitiveDataFile))

    if (sensitiveData != null) {
        if (sensitiveData.containsKey('youtubeApiKey')) youtubeApiKey = sensitiveData['youtubeApiKey']
    }
}

// In this section a reviewer can insert directly his own sensitive data
// by replacing the strings beginning with REVIEWERS_
// Just double click on the string and paste you own data
if (youtubeApiKey == noKeyFound) youtubeApiKey = '"REVIEWERS_YOUTUBE_API_KEY_GOES_HERE"'
// End of reading the sensitive data

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.aiassoft.capstone"
        minSdkVersion 19 // kitkat
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        // https://chris.banes.me/2016/02/25/appcompat-vector/#enabling-the-flag
        vectorDrawables.useSupportLibrary = true

        // Begin of injecting the sensitive data
        // Add the sensitive data as Constant Fields to the BuildConfig class
        buildConfigField "String", "YOUTUBE_API_KEY", youtubeApiKey
        // Add the sensitive data as String Resources
        resValue "string", "YOUTUBE_API_KEY", youtubeApiKey
        // End of injecting the sensitive data

        resValue "string", "ANDROID_SUPPORT_VERSION", androidSupportVersion
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"

    implementation "com.android.support:support-annotations:$androidSupportVersion"

    implementation 'com.google.android.gms:play-services-ads:12.0.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Butter Knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

}
